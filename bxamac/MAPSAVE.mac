.*                                                                      00000100
.* This macro is free software; you can redistribute it and/or modify   00000200
.* it under the terms of the GNU General Public License as published by 00000300
.* the Free Software Foundation; either version 2 of the License        00000400
.* or (at your option) any later version.                               00000500
.* The license text is available at the following internet addresses:   00000600
.* - http://www.bixoft.com/english/gpl.htm                              00000700
.* - http://fsf.org                                                     00000800
.* - http://opensource.org                                              00000900
.*                                                                      00001000
.* This macro is distributed in the hope that it will be useful,        00001100
.* but WITHOUT ANY WARRANTY; without even the implied warranty of       00001200
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                 00001300
.* See the GNU General Public License for more details.                 00001400
.*                                                                      00001500
.* You should have received a copy of the GNU General Public License    00001600
.* along with this program; if not, write to either of the following:   00001700
.* the Free Software Foundation, Inc.      B.V. Bixoft                  00001800
.* 59 Temple Place, Suite 330              Rogge 9                      00001900
.* Boston, MA 02111-1307                   7261 JA Ruurlo               00002000
.* United States of America                The Netherlands              00002100
.*                                                                      00002200
.*                                         e-mail: bixoft@bixoft.nl     00002300
.*                                         phone : +31-6-22755401       00002400
.*                                                                      00002500
.********************************************************************** 00010000
.*                                                                      00020000
.* Bixoft eXtended Assembly language                                    00030000
.* Licensed material - Property of B.V. Bixoft                          00040000
.*                                                                      00050000
.* This macro can be licensed or used on an as-is basis.                00060000
.* No warranty, neither implicit nor explicit, is given.                00070000
.* It remains your own responsibility to ensure the correct             00080000
.* working of any program using this macro.                             00090000
.*                                                                      00100000
.* Suggestions for improvement are always welcome at                    00110000
.* http://www.bixoft.com  or mail to  bixoft@bixoft.nl                  00120000
.*                                                                      00130000
.* (C) Copyright B.V. Bixoft, 1999                                      00140000
.********************************************************************** 00150000
         MACRO                                                          00160000
.*                                                                      00170000
.* Mapping macro for standard MVS save-areas                            00180000
.*           and for Bixoft internal/external save-areas                00190000
.*                                                                      00200000
&LABEL   MAPSAVE &DSECT=YES,           * YES or NO                     *00210000
               &CB=,                   * Control block to generate     *00220000
               &PRFX=                  * Prefix to use                  00230000
.********************************************************************** 00231001
.*                                                                      00232001
.*       IMPORTANT NOTICE                                               00233001
.*       ========= ======                                               00234001
.*                                                                      00235001
.* Code below checks whether 'USER' accepted the terms and conditions   00236001
.* of the license for the BXA macro library. This code is to be treated 00237001
.* as part of the Copyright Notice and therefore may not be changed     00238001
.* or disabled in any way.                                              00239001
.*                                                                      00239101
.********************************************************************** 00239201
         GBLA  &BXA_RC                 * Returncode from CHKLIC         00239301
         CHKLIC MAPSAVE                * Check license acceptance       00239401
         AIF   (&BXA_RC NE 0).MEND                                      00239502
.********************************************************************** 00239601
.*                                                                      00239701
.* End of special code that is part of the Copyright Notice             00239801
.*                                                                      00239901
.********************************************************************** 00240001
.*                                                                      00241000
.* Declare variables                                                    00250000
         GBLB  &BXA_MAC_MAPSAVE        * Map has been generated?        00260000
.*                                                                      00280000
.* Check parameters                                                     00290000
&LABEL   CHKMAP DSECT=&DSECT,          *                               *00300000
               CB=&CB,                 *                               *00310000
               PRFX=&PRFX              *                                00320000
         AIF   (&BXA_RC GT 4).MEND                                      00330002
.*                                                                      00340000
.* Check the CB parameter                                               00350000
         AIF   ('&DSECT' EQ 'YES').DSECT                                00360000
         AIF   ('&CB' EQ 'BXASAVE').EMBED                               00370000
         AIF   ('&CB' EQ 'SAVEAREA').EMBED                              00380000
.ERR5    MNOTE 8,'&CB control block not supported'                      00390000
         MEXIT                                                          00400000
.*                                                                      00410000
.* Generate embedded definition                                         00420000
.EMBED   ANOP                                                           00430000
&LABEL   NESTCB &CB,X,&PRFX                                             00440000
         MEXIT                                                          00450000
.*                                                                      00460000
.DSECT   ANOP                                                           00470000
         AIF   (&BXA_MAC_MAPSAVE).MEND                                  00480002
&BXA_MAC_MAPSAVE SETB 1                                                 00490000
.* Set up translation from control block to macro for DCL               00500000
         GBLC  &BXA_CB_BXASAVE                                          00510000
         GBLC  &BXA_CB_SAVEAREA                                         00520000
&BXA_CB_BXASAVE  SETC 'SAVE'                                            00530000
&BXA_CB_SAVEAREA SETC 'SAVE'                                            00540000
.*                                                                      00550000
         MNOTE *,'Generating BXASAVE Dsect'                             00560000
*                                                                       00570000
* DSECT for save areas                                                  00580000
*                                                                       00590000
* Issue USING SAVEAREA,R13                                              00600000
*                                                                       00610000
* In ESTAE-type subroutines, 2 internal SA's are used to copy the       00620000
* contents of the external SA, and the save info from entry into the    00630000
* ESTAE-type subroutine. The external SA is thus available again for    00640000
* any invoked functions that require R13 to point to a save area.       00650000
*                                                                       00660000
BXASAVE  DSECT ,                       *                                00670000
SAVEPRFX DS    0XL16                   * Bixoft prefix for save-areas   00680000
SAVEID   DS    CL8                     * Only used by Bixoft, valid     00690000
*                                      *   only for external SA's and   00700000
*                                      *   on 1st SA of an SA-subchain  00710000
SAVEPTPT DS    A                       * Ptr to ptr to this area        00720000
SAVEPTSA DS    A                       * Ptr to SA used on entry        00730000
SAVEPRFX_LEN EQU *-SAVEPRFX            * Length of prefix               00740000
*                                                                       00750000
SAVEAREA DS    0XL72                   * Standard save-area             00760000
SAVEPTRS DS    0XL12                   * Define first 3 words at once   00770000
SAVEHDR  DS    F                       * External SA: reserved for PL1 *00780000
                                       *     for SRB: saved ret.addr.  *00790000
                                       * Internal SA: address of BEGSR  00800000
SAVEPREV DS    F                       * Backward pointer               00810000
SAVEF1SA EQU   SAVEPREV,,C'C'          * Character value F1SA in first *00820000
                                       *   savearea on chain            00830000
SAVENEXT DS    F                       * Forward pointer                00840000
SAVEDR13 EQU   SAVEPREV                * Saved value of reg 13          00850000
SAVEDR14 DS    F                       * Saved value of reg 14          00860000
SAVEDR15 DS    F                       * Saved value of reg 15          00870000
SAVEDR0  DS    F                       * Saved value of reg 0           00880000
SAVEDR1  DS    F                       * Saved value of reg 1           00890000
SAVEDR2  DS    F                       * Saved value of reg 2           00900000
SAVEDR3  DS    F                       * Saved value of reg 3           00910000
SAVEDR4  DS    F                       * Saved value of reg 4           00920000
SAVEDR5  DS    F                       * Saved value of reg 5           00930000
SAVEDR6  DS    F                       * Saved value of reg 6           00940000
SAVEDR7  DS    F                       * Saved value of reg 7           00950000
SAVEDR8  DS    F                       * Saved value of reg 8           00960000
SAVEDR9  DS    F                       * Saved value of reg 9           00970000
SAVEDR10 DS    F                       * Saved value of reg 10          00980000
SAVEDR11 DS    F                       * Saved value of reg 11          00990000
SAVEDR12 DS    F                       * Saved value of reg 12          01000000
*                                                                       01010000
SAVEAREA_END EQU *                     * End of standard MVS save-area  01020000
SAVEAREA_LEN EQU *-SAVEAREA            * Length of standard save-area   01030000
*                                                                       01040000
* Following fields are used only by Bixoft. These are defined only for  01050000
*    external save-areas, and serve to tie the chain of internal SA's   01060000
*    to the external SA.                                                01070000
*                                                                       01080000
SAVEINT  DS    0XL8                    * Defines both pointers at once  01090000
SAVEINTF DS    A                       * Pointer to first internal SA   01100000
SAVEINTU DS    A                       * Pointer to last-used int. SA   01110000
*                                                                       01120000
         ORG   ,                       *                                01130000
         DS    0D                      * Align for internal Save areas  01140000
BXASAVE_LEN EQU *-BXASAVE              * Length of complete DSECT       01150000
.*                                                                      01160000
.* Return to our original CSECT name                                    01170000
&SYSECT  &SYSSTYP                                                       01180000
.*                                                                      01190000
.MEND    MEND                                                           01210002
