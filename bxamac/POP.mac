.*                                                                      00000100
.* This macro is free software; you can redistribute it and/or modify   00000200
.* it under the terms of the GNU General Public License as published by 00000300
.* the Free Software Foundation; either version 2 of the License        00000400
.* or (at your option) any later version.                               00000500
.* The license text is available at the following internet addresses:   00000600
.* - http://www.bixoft.com/english/gpl.htm                              00000700
.* - http://fsf.org                                                     00000800
.* - http://opensource.org                                              00000900
.*                                                                      00001000
.* This macro is distributed in the hope that it will be useful,        00001100
.* but WITHOUT ANY WARRANTY; without even the implied warranty of       00001200
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                 00001300
.* See the GNU General Public License for more details.                 00001400
.*                                                                      00001500
.* You should have received a copy of the GNU General Public License    00001600
.* along with this program; if not, write to either of the following:   00001700
.* the Free Software Foundation, Inc.      B.V. Bixoft                  00001800
.* 59 Temple Place, Suite 330              Rogge 9                      00001900
.* Boston, MA 02111-1307                   7261 JA Ruurlo               00002000
.* United States of America                The Netherlands              00002100
.*                                                                      00002200
.*                                         e-mail: bixoft@bixoft.nl     00002300
.*                                         phone : +31-6-22755401       00002400
.*                                                                      00002500
.********************************************************************** 00010000
.*                                                                      00020000
.* Bixoft eXtended Assembly language                                    00030000
.* Licensed material - Property of B.V. Bixoft                          00040000
.*                                                                      00050000
.* This macro can be licensed or used on an as-is basis.                00060000
.* No warranty, neither implicit nor explicit, is given.                00070000
.* It remains your own responsibility to ensure the correct             00080000
.* working of any program using this macro.                             00090000
.*                                                                      00100000
.* Suggestions for improvement are always welcome at                    00110000
.* http://www.bixoft.com  or mail to  bixoft@bixoft.nl                  00120000
.*                                                                      00130000
.* (C) Copyright B.V. Bixoft, 1999                                      00140000
.********************************************************************** 00150000
         MACRO                                                          00160000
.*                                                                      00170000
.* This macro replaces the assembler's POP statement                    00180000
.*                                                                      00190000
&LABEL   POP   ,                       * Arguments in SYSLIST           00200000
.********************************************************************** 00201001
.*                                                                      00202001
.*       IMPORTANT NOTICE                                               00203001
.*       ========= ======                                               00204001
.*                                                                      00205001
.* Code below checks whether 'USER' accepted the terms and conditions   00206001
.* of the license for the BXA macro library. This code is to be treated 00207001
.* as part of the Copyright Notice and therefore may not be changed     00208001
.* or disabled in any way.                                              00209001
.*                                                                      00209101
.********************************************************************** 00209201
         GBLA  &BXA_RC                 * Returncode from CHKLIC         00209301
         CHKLIC POP                    * Check license acceptance       00209401
         AIF   (&BXA_RC NE 0).MEND                                      00209502
.********************************************************************** 00209601
.*                                                                      00209701
.* End of special code that is part of the Copyright Notice             00209801
.*                                                                      00209901
.********************************************************************** 00210001
.*                                                                      00211000
.* Declare variables                                                    00220000
         GBLB  &BXA_USED_REGS(16)      * Use status of regs R0-R15      00230000
         GBLB  &SP_SHOWALL             * SHOWALL option                 00240000
         GBLC  &BXA_USELBL(50)         * Using labels ...               00250000
         GBLA  &BXA_USEREG(50)         *   and their register indexes   00260000
         GBLC  &BXA_USEFLD(50)         *   and associated base fields   00270000
         GBLA  &BXA_USENDX0(5)         * Low valid pointers BXA_USE...  00280000
         GBLA  &BXA_USENDX1(5)         * High valid pointers BXA_USE... 00290000
         GBLA  &BXA_USENDX             * Index into &BXA_USENDX0/1      00300000
         LCLA  &N                      * Index for BXA_USE...           00310000
         LCLA  &I                      * Index into &SYSLIST            00320000
         LCLA  &R                      * Index into &BXA_USED_REGS      00330000
         LCLC  &ARG                    * 1 argument from SYSLIST        00340000
         LCLC  &ARGS                   * Arguments from SYSLIST         00350000
         LCLB  &POPUSE                 * USING status is to be popped   00360000
.*                                                                      00370000
.* Build ARGS from &SYSLIST                                             00380000
&I       SETA  0                       * I indexes &SYSLIST             00390000
.LOOP1   ANOP  ,                       * For all entries in SYSLIST     00400000
&I       SETA  &I+1                    * Point next entry               00410000
         AIF   (&I GT N'&SYSLIST).LOOP1OK * At end: quit loop           00420000
&ARG     SETC  '&SYSLIST(&I)'          * Extract 1 argument             00430000
&ARGS    SETC  '&ARGS'.','.'&SYSLIST(&I)' * Add entry to ARGS           00440000
         AIF   ('&ARG' NE 'USING').LOOP1 * Not USING: get next arguemnt 00450000
&POPUSE  SETB  1                       * Indicate USING is popped       00460000
         AGO   .LOOP1                  * And go process next entry      00470000
.LOOP1OK ANOP                                                           00480000
         AIF   (K'&ARGS LT 2).ARGSOK                                    00490000
&ARGS    SETC  '&ARGS'(2,*)            * Remove leading comma           00500000
.ARGSOK  ANOP                                                           00510000
.*                                                                      00520000
.* If Using status is being popped:                                     00530000
.* pop the table of active usings and rebuild &BXA_USED_REGS            00540000
         AIF   (NOT &POPUSE).NOTUSE                                     00550000
         AIF   (&BXA_USENDX LE 0).ERR2A                                 00560000
&BXA_USENDX SETA &BXA_USENDX-1         * Pop table of active usings     00570000
.*                                                                      00580000
.* Rebuild BXA_USED_REGS from BXA_USEREG table                          00590000
.* Skip any USINGs for PSA,R0                                           00600000
&BXA_USED_REGS(1) SETB 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0                  00610000
&N       SETA  &BXA_USENDX0(&BXA_USENDX) * Search tables to mark        00620000
.LOOP4   ANOP  ,                       *    all used registers          00630000
&N       SETA  &N+1                    *    unavailable.                00640000
         AIF   (&N GT &BXA_USENDX1(&BXA_USENDX)).LOOP4OK * At end: done 00650000
&R       SETA  &BXA_USEREG(&N)         * Obtain index                   00660000
         AIF   (&R EQ 1 AND '&BXA_USEFLD(&N)' EQ 'PSA').LOOP4           00670000
&BXA_USED_REGS(&R) SETB 1              * Set designated register in use 00680000
         AGO   .LOOP4                                                   00690000
.LOOP4OK ANOP                                                           00700000
         AGO   .NOTUSE                 * Skip error messages            00710000
.*                                                                      00720000
.ERR2A   MNOTE 8,'You''re POPping an unPUSHed USING status'             00730000
.NOTUSE  ANOP                                                           00740000
.*                                                                      00750000
.* Generate _POP statement                                              00760000
&LABEL   _POP  &ARGS                                                    00770000
.*                                                                      00780000
.* Report current USING status                                          00790000
         AIF   (NOT &SP_SHOWALL).MEND * Only if SHOWALL requested       00800002
         AIF   (NOT &POPUSE).MEND      * And only if USING was popped   00810002
         USEDREGS                                                       00820000
.*                                                                      00830000
.MEND    MEND                                                           00850002
